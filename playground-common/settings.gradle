/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def includeProject(name, filePath) {
    settings.include(name)

    def file
    if (filePath instanceof String) {
        file = new File(rootDir, filePath)
    } else {
        file = filePath
    }
    if (!file.exists()) {
        // This option is supported so that development/simplify_build_failure.sh can try
        // deleting entire projects at once to identify the cause of a build failure
        if (System.getenv("ALLOW_MISSING_PROJECTS") == null) {
            throw new Exception("Path " + file + " does not exist; cannot include project " + name)
        }
    }
    project(name).projectDir = file
}

def setupPlayground(settings) {
    settings.pluginManagement {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }
    }
    settings.includeBuild("../androidx-plugin")

    settings.includeProject(":lint-checks", "../lint-checks")
    settings.includeProject(":internal-testutils-common", "../testutils/testutils-common")
    settings.includeProject(":internal-testutils-gradle-plugin", "../testutils/testutils-gradle-plugin")
}
ext.includeProject = this.&includeProject
ext.setupPlayground = this.&setupPlayground